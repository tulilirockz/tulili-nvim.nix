# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.10)
{
  description = "Portable nixvim configuration for Tulilirockz";
  inputs = {
    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/*.tar.gz";
    nixvim = {
      url = "github:nix-community/nixvim";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nix-colors.url = "github:misterio77/nix-colors";
    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/0.1.*.tar.gz";
  };
  outputs =
    {
      self,
      flake-schemas,
      nixpkgs,
      nixvim,
      nix-colors,
    }:
    let
      supportedSystems = [
        "x86_64-linux"
        "aarch64-darwin"
        "x86_64-darwin"
        "aarch64-linux"
      ];
      forEachSupportedSystem =
        f: nixpkgs.lib.genAttrs supportedSystems (system: f { pkgs = import nixpkgs { inherit system; }; });
    in
    {
      packages = forEachSupportedSystem (
        { pkgs }:
        rec {
          neovim = nixvim.legacyPackages.${pkgs.system}.makeNixvimWithModule {
            inherit pkgs;
            module = import ./main/main.nix {
              inherit pkgs;
              config = {
                colorScheme.palette = nix-colors.colorScheme.catppucin;
              };
            };
          };
          default = neovim;
          nvim = neovim;
        }
      );

      schemas = flake-schemas.schemas;

      formatter = forEachSupportedSystem ({ pkgs }: pkgs.nixfmt-rfc-style);

      devShells = forEachSupportedSystem (
        { pkgs }:
        {
          default = pkgs.mkShell {

            packages = with pkgs; [
              curl
              jq
              nixpkgs-fmt
            ];
          };
        }
      );
    };
}
